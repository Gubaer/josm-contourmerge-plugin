plugins {
    // uses a local development fork of the gradle josm plugin.
    // This should be
    //   id 'gradle-josm-plugin'
    // but: This is a workaround for a bug in the build/publish script of the
    // gradle-josm-plugin. If it is published to a maven repo hosted on S3,
    // the plugin jar includes a file 'META-INF/gradle-plugins/org.openstreetmap.josm.properties'
    // instead of a file 'META-INFO/gradle-plugins/gradle-josm-plugin.properties'.
    // We therefore have to use the plugin ID 'org.openstreetmap.josm' instead
    // of 'gradle-josm-plugin'.
    // See also 'settings.gradle' for additional configuration settings.
    id 'org.openstreetmap.josm'
    id 'java'
    id 'groovy'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

dependencies {

    compileOnly "org.projectlombok:lombok:1.18.2"
    annotationProcessor "org.projectlombok:lombok:1.18.2"
    packIntoJar "org.apache.commons:commons-lang3:3.7"

    implementation "javax.validation:validation-api:2.0.1.Final"
    testImplementation "junit:junit:4.+"
    testImplementation "org.codehaus.groovy:groovy-all:2.4.13"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
}

version="v0.1.6"
archivesBaseName = "contourmerge"

josm {
    josmCompileVersion = "latest"
    manifest {
        includeLinksToGithubReleases = true
        minJosmVersion = 14654
    }

    github {
        repositoryName = "josm-contourmerge-plugin"
        repositoryOwner = "gubaer"
    }
}

compileJava {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

test {
    useJUnit()
    systemProperty "josm.home", file("src/test/resources/josm.home")

    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with " + 
                "result: ${result.resultType}"
    }
}

sourceSets {
    main.resources {
        exclude "images/**/*.svg"
    }
}

processResources {
    from("$projectDir/README.md")
    from("$projectDir/LICENSE")
}

publishToGithubRelease {
    remoteJarName = "contourmerge.jar"
}